# Maintainer: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daurnimator <quae@daurnimator.com>

pkgname=libidn2-glibc
pkgver=2.3.3
pkgrel=0
pkgdesc="Free software implementation of IDNA2008, Punycode and TR46"
arch=(any)
url="https://www.gnu.org/software/libidn/#libidn2"
license=('GPL2' 'LGPL3')
makedepends=('ruby-ronn')
source=(https://ftp.gnu.org/gnu/libidn/libidn2-$pkgver.tar.gz{,.sig})
sha256sums=('f3ac987522c00d33d44b323cae424e2cffcb4c63c6aa6cd1376edacbf1c36eb0'
            'SKIP')
validpgpkeys=('B1D2BD1375BECB784CF4F8C4D73CF638C53C06BE') # Simon Josefsson <simon@josefsson.org>

build() {
  cd libidn2-$pkgver

  case "$CARCH" in
    "aarch64")
      CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-aarch64.so.1"
      CXXFLAGS="${CFLAGS}"
      ;;
    "armv7h")
      CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-armhf.so.3"
      CXXFLAGS="${CFLAGS}"
      ;;
  esac

  ./configure \
    --prefix=/data/data/com.termux/files/usr/glibc \
    --disable-static
  make
}

package() {
  cd libidn2-$pkgver
  make DESTDIR="$pkgdir" install
  rm -f "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/info/dir
}

# vim:set ts=2 sw=2 et:
