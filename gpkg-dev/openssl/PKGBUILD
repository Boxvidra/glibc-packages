# The original PKGBUILD was taken from the archlinux/svntogit-packages repo:
# - https://github.com/archlinux/svntogit-packages/blob/packages/openssl/trunk/PKGBUILD

pkgname=openssl-glibc
pkgver=3.0.8
pkgrel=0
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=(any)
url='https://www.openssl.org'
license=('custom:BSD')
makedepends=('perl')
backup=('data/data/com.termux/files/usr/glibc/etc/ssl/openssl.cnf')
source=("https://www.openssl.org/source/openssl-${pkgver}.tar.gz"
	'ca-dir.patch')
sha256sums=('6c13d2bf38fdf31eac3ce2a347073673f5d63263398f1f69d0df4a41253e4b3e'
            '326478a21829b79710ce0d8f7437e3be10c542471fef3bdf5f70ac232a5bf9e8')
groups=('gpkg-dev')

prepare() {
	cd "$srcdir/openssl-$pkgver"

	# set ca dir to /etc/ssl by default
	patch -Np1 -i "$srcdir/ca-dir.patch"
}

build() {
	cd "$srcdir/openssl-$pkgver"

	case "$CARCH" in
		"aarch64")
			CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-aarch64.so.1"
			CXXFLAGS="${CFLAGS}"
			;;
		"armv7h")
			CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-armhf.so.3"
			CXXFLAGS="${CFLAGS}"
			;;
		"x86_64")
			CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-x86-64.so.2"
			CXXFLAGS="${CFLAGS}"
			;;
		"i686")
			CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux.so.2"
			CXXFLAGS="${CFLAGS}"
			;;
	esac

	if [ "${CARCH}" = "armv7h" ]; then
		openssltarget='linux-armv4'
		optflags=''
	elif [ "${CARCH}" = "aarch64" ]; then
		openssltarget='linux-aarch64'
		optflags='no-afalgeng'
	elif [ "${CARCH}" = "x86_64" ]; then
		openssltarget='linux-x86_64'
		optflags='enable-ec_nistp_64_gcc_128'
	elif [ "${CARCH}" = "i686" ]; then
		openssltarget='linux-generic32'
		optflags='no-sse2'
	fi

	export PATH=/usr/bin/core_perl:$PATH

	# mark stack as non-executable: http://bugs.archlinux.org/task/12434
	./Configure --prefix=/data/data/com.termux/files/usr/glibc --openssldir=/data/data/com.termux/files/usr/glibc/etc/ssl --libdir=lib \
		shared enable-ktls ${optflags} "${openssltarget}" \
		"-Wa,--noexecstack ${CPPFLAGS} ${CFLAGS} ${LDFLAGS}"

	make depend
	make
}

package() {
	cd "$srcdir/openssl-$pkgver"

	make DESTDIR="$pkgdir" MANDIR=/data/data/com.termux/files/usr/glibc/share/man MANSUFFIX=ssl install_sw install_ssldirs install_man_docs

	install -D -m644 LICENSE.txt "$pkgdir/data/data/com.termux/files/usr/glibc/share/licenses/$pkgname/LICENSE"
}
