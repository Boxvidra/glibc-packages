# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=curl
pkgname=(curl-glibc libcurl-compat-glibc)
pkgver=8.0.1
pkgrel=0
pkgdesc='An URL retrieval utility and library'
arch=(any)
url='https://curl.haxx.se'
license=('MIT')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.gz"{,.asc})
sha512sums=('87d945e65176613c6296f8b130bd6c5355e3ca3c62ee4010edd481950cad9760482fd3be8a84e51501c118c29ebc284b8f87c49b06f9d248e9da5819fda38f8f'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg
groups=('gpkg-dev')

_configure_options=(
  --prefix='/data/data/com.termux/files/usr/glibc'
  --mandir='/data/data/com.termux/files/usr/glibc/share/man'
  --disable-ldap
  --disable-ldaps
  --disable-manual
  --enable-ipv6
  --enable-threaded-resolver
  --with-libssh2
  --with-openssl
  --with-gssapi
  --with-random='/dev/urandom'
  --with-ca-bundle='/data/data/com.termux/files/usr/glibc/etc/ssl/certs/ca-certificates.crt'
)

prepare() {
  cd ${pkgbase}-${pkgver}
  sed -i "/\WLIBCURL_VERSION\W/c #define LIBCURL_VERSION \"${pkgver}\"" include/curl/curlver.h
  autoreconf -fi
}

build() {
  mkdir build-curl{,-compat} || true

  # build curl
  cd "${srcdir}"/build-curl

  case "$CARCH" in
    "aarch64")
      CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-aarch64.so.1"
      CXXFLAGS="${CFLAGS}"
      ;;
    "armv7h")
      CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-armhf.so.3"
      CXXFLAGS="${CFLAGS}"
      ;;
  esac

  "${srcdir}/${pkgbase}-${pkgver}"/configure \
    "${_configure_options[@]}" \
    --enable-versioned-symbols
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  # build libcurl-compat
  cd "${srcdir}"/build-curl-compat

  "${srcdir}/${pkgbase}-${pkgver}"/configure \
    "${_configure_options[@]}" \
    --disable-versioned-symbols
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C lib
}

package_curl-glibc() {
  cd build-curl

  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install -C scripts

  cd "${srcdir}/curl-${pkgver}"

  # license
  install -Dt "${pkgdir}/data/data/com.termux/files/usr/glibc/share/licenses/curl" -m0644 COPYING
}

package_libcurl-compat-glibc() {
  pkgdesc='An URL retrieval library (without versioned symbols)'

  cd "${srcdir}"/build-curl-compat

  make -C lib DESTDIR="${pkgdir}" install

  mv "${pkgdir}"/data/data/com.termux/files/usr/glibc/lib/libcurl{,-compat}.so.4.8.0
  rm "${pkgdir}"/data/data/com.termux/files/usr/glibc/lib/libcurl.{a,so}*
  for version in 3 4.0.0 4.1.0 4.2.0 4.3.0 4.4.0 4.5.0 4.6.0 4.7.0; do
    ln -s libcurl-compat.so.4.8.0 "${pkgdir}"/data/data/com.termux/files/usr/glibc/lib/libcurl.so.${version}
  done

  install -dm 0755 "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/licenses
  ln -s curl "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/licenses/libcurl-compat
}
