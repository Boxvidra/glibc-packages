# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Andrew Sun  <adsun701 at gmail dot com>
# Contributor: Kritias     <theodoridisgr at gmail dot com>
# Contributor: sudokode    <sudokode at gmail dot com>
# Contributor: Allan McRae <allan at archlinux dot org>

pkgname=libisl-glibc
pkgver=0.25
pkgrel=1
pkgdesc='Library for manipulating sets and relations of integer points bounded by linear constraints'
arch=('any')
url='https://libisl.sourceforge.io'
license=('MIT')
options=(staticlibs)
source=("https://libisl.sourceforge.io/isl-${pkgver}.tar.xz")
b2sums=('54a13ba8a5d7666f62f3fb20db278d15b803400cb8b69f119b18fd1f54f7d02f1e07cd1e189803fc416afaa81b35e2247ef6b3da3d8cddc463b05cc9b3921890')

build() {
  cd "${srcdir}"/isl-${pkgver}
  ./configure --prefix=/data/data/com.termux/files/usr/glibc
  make
}

package() {
  cd "${srcdir}"/isl-${pkgver}

  make DESTDIR="${pkgdir}" install

  install -dm755 "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/gdb/auto-load/usr/lib/
  mv "${pkgdir}"/data/data/com.termux/files/usr/glibc/lib/libisl.so.*-gdb.py "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/gdb/auto-load/usr/lib/
  
  install -Dm644 LICENSE "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/licenses/isl/LICENSE
}
