# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Darshit Shah <darnir@gmail.com>

pkgname=libpsl-glibc
pkgver=0.21.2
pkgrel=0
pkgdesc='Public Suffix List library'
url='https://github.com/rockdaboot/libpsl'
arch=(any)
license=('MIT')
makedepends=('libxslt' 'python' 'publicsuffix-list' 'gtk-doc')
source=(https://github.com/rockdaboot/libpsl/releases/download/${pkgver}/libpsl-${pkgver}.tar.gz)
sha512sums=('f1df72220bf4391d4701007100b0df66c833a2cbcb7481c9d13f0b9e0cad3b66d2d15d4b976e5bad60d2ad1540355112fa1acb07aa925c241d2d7cd20681c71d')
b2sums=('b50f805bb467dc3284bc91645a37a2208098ad809d3771c74ef13c3b8f6029a07ad80a56702c7e3d1a1125d272403c85dd97b64a28465b9ff2d095eaf94b9a4d')
groups=('gpkg-dev')

prepare() {
  cd libpsl-${pkgver}
  rm -frv list
  autoreconf -fiv
}

build() {
  cd libpsl-${pkgver}

  case "$CARCH" in
    "aarch64")
      CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-aarch64.so.1"
      CXXFLAGS="${CFLAGS}"
      ;;
    "armv7h")
      CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-armhf.so.3"
      CXXFLAGS="${CFLAGS}"
      ;;
  esac

  ./configure \
    --prefix=/data/data/com.termux/files/usr/glibc \
    --disable-static \
    --disable-dependency-tracking \
    --enable-man \
    --enable-gtk-doc \
    --enable-{builtin,runtime}=libidn2 \
    --with-psl-file=/data/data/com.termux/files/usr/glibc/share/publicsuffix/effective_tld_names.dat \
    --with-psl-testfile=/data/data/com.termux/files/usr/glibc/share/publicsuffix/test_psl.txt
  LC_CTYPE=en_US.UTF-8 make
}

package() {
  cd libpsl-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/data/data/com.termux/files/usr/glibc/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
