# Maintainer: Anatol Pomozov
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Christer Solskogen <christer.solskogen@gmail.com>

pkgname=glibc
pkgver=2.37
pkgrel=0
_commit=be176490b818b65b5162c332eb6b581690b16e5c
pkgdesc="GNU C Library"
arch=(any)
url='https://www.gnu.org/software/libc/'
license=('GPL' 'LGPL')
options=(!strip staticlibs)
source=(https://ftp.gnu.org/gnu/libc/glibc-$pkgver.tar.xz{,.sig}
	'_Fork.c.patch'
	'dl-tls_init_tp.c.patch'
	'pthread_create.c.patch'
        'faccessat.c.patch'
        'tst-rseq-disable.c.patch'
        'tst-rseq.h.patch'
        'rseq-internal.h.patch'
        'disable-clone3.patch'
        'locale-gen'
        'locale.gen.txt'
        'sdt-config.h'
        'sdt.h'
	'grun'
	'statx.c.patch'
	'xstat64.c.patch'
	'lxstat64.c.patch'
	'fxstatat64.c.patch'
	'fxstat64.c.patch'
	'fstatat64.c.patch'
	'clock_gettime.c.patch')
sha256sums=('2257eff111a1815d74f46856daaf40b019c1e553156c69d48ba0cbfc1bb91a43'
            'SKIP'
            '74da3d03fb81e2511140411bc8bad2a893258a2465c5eec5ac955dccddf9621d'
            '9223be94da6667f0dff0bf7afb032932dd9a151f23af178faa4d1d7f26c58608'
            'dd0f14f9ccca83424bc2b841346047d16c59d229c1a459fc764e6c67f4fe91ba'
            '6814b1c02455fc482d7ea340112bcca1cdd4e13ca2c16f156654d7a65dbdcda2'
            '27e1ea17f40c57f6bac2e46816d0684170818b58ecced7a83efb8e7f0f038af0'
            'b6286fe9ffd9707d171db5dd53b3c084d7d38402e1a32eb8b9bd455d9a8b5ab6'
            'ec053954ab9e19b850227048a61e622bd15d6a651fd7498e507e908485374299'
            '0f778f5a2d20e8710647370e89c793eb27bbf8ef9824558d0154185c91ecd12c'
            'cdd916c0dc29e5d958ddef5c3530947244fd740ce7645601bfabae53512323b8'
            'd42648cea552ba5353a32e264686e992263289d5cc86207314dffc54ab514981'
            'cdc234959c6fdb43f000d3bb7d1080b0103f4080f5e67bcfe8ae1aaf477812f0'
            '774061aff612a377714a509918a9e0e0aafce708b87d2d7e06b1bd1f6542fe70'
            '016a827f4876283a1f169b2f8849b077450ba5903d8f4c7ff3c2737df4d8ffdc'
            'e0f6a51cbd64a30394d6a22e5e14603766ac924e9b6690adb673517821c2015e'
            'e67383acf8935a68353f0f1c786161222fbb08c9255cffa3ccb06565e1401cbc'
            'cd981ed4c8b77ee1551ae5a017dcc9b7891a6078450633eafe528e3984254f40'
            'e6b3bac73759e231cfa0099be3ca50f7b5a379ecdac18a598999e87d51c8f249'
            '4de3605da9e6f907315eb87c58c529faf6f703518daa6fe060c51715c5a0e455'
            'a6ea8efc55de0e26494bf8839ae32d95c36b6956788535a9a8912ad989510f36'
            '18f01ed2c6ed8acb122adc443fd428c8a37bc6a7d518f4e433e2ee462eebd5eb')
validpgpkeys=(7273542B39962DF7B299931416792B4EA25340F8  # "Carlos O'Donell <carlos@systemhalted.org>"
              BC7C7372637EC10C57D7AA6579C43DFBF1CF2187) # Siddhesh Poyarekar
groups=('gpkg-dev')

prepare() {
  for i in *.patch; do
    patch -Np1 -i ${srcdir}/$i
  done

  mkdir -p glibc-build

  [[ ! -d glibc ]] && ln -s glibc-$pkgver glibc

  sed -i 's|/tmp|/data/data/com.termux/files/usr/tmp|g' $(grep -R /tmp/ ${srcdir}/glibc-${pkgver} | grep -v /var/ | awk -F'[:=]' '{printf $1 "\n"}')
  sed -i 's|/var/tmp|/data/data/com.termux/files/usr/tmp|g' $(grep -R /var/tmp ${srcdir}/glibc-${pkgver} | awk -F'[:=]' '{printf $1 "\n"}')

  if [ -f ${srcdir}/glibc-${pkgver}/sysdeps/unix/sysv/linux/generic/chmod.c ]; then
    mv ${srcdir}/glibc-${pkgver}/sysdeps/unix/sysv/linux/generic/chmod.c ${srcdir}/glibc-${pkgver}/sysdeps/unix/sysv/linux/chmod.c
  fi
}

build() {
  cd glibc-build
  echo "slibdir=/data/data/com.termux/files/usr/glibc/lib" > configparms
  echo "rtlddir=/data/data/com.termux/files/usr/glibc/lib" >> configparms
  echo "sbindir=/data/data/com.termux/files/usr/glibc/bin" >> configparms
  echo "rootsbindir=/data/data/com.termux/files/usr/glibc/bin" >> configparms

  unset CFLAGS
  ../glibc-$pkgver/configure \
      --prefix=/data/data/com.termux/files/usr/glibc \
      --libdir=/data/data/com.termux/files/usr/glibc/lib \
      --libexecdir=/data/data/com.termux/files/usr/glibc/lib \
      --with-bugurl=https://github.com/Maxython/glibc-for-termux/issues \
      --enable-bind-now \
      --enable-cet \
      --enable-multi-arch \
      --enable-stack-protector=strong \
      --enable-systemtap \
      --disable-profile \
      --disable-crypt \
      --disable-werror CFLAGS="-O2 -pipe"

  echo "build-programs=no" >> configparms
  make -O

  sed -i "/build-programs=/s#no#yes#" configparms
  echo "CFLAGS=-O2 -pipe" >> configparms
  make -O
  make info

  localedef -c -f ../glibc/localedata/charmaps/UTF-8 -i ../glibc/localedata/locales/C ../C.UTF-8/
}

package() {
  backup=(data/data/com.termux/files/usr/glibc/etc/gai.conf
          data/data/com.termux/files/usr/glibc/etc/locale.gen
          data/data/com.termux/files/usr/glibc/etc/nscd.conf)

  make -C glibc-build install_root="$pkgdir" install
  rm -f "$pkgdir"/data/data/com.termux/files/usr/glibc/etc/ld.so.{cache,conf}

  # Shipped in tzdata
  rm -f "$pkgdir"/usr/bin/{tzselect,zdump,zic}

  cd glibc

  install -dm755 "$pkgdir"/data/data/com.termux/files/usr/glibc/lib/{locale,systemd/system,tmpfiles.d}
  install -m644 nscd/nscd.conf "$pkgdir/data/data/com.termux/files/usr/glibc/etc/nscd.conf"
  install -m644 nscd/nscd.service "$pkgdir/data/data/com.termux/files/usr/glibc/lib/systemd/system"
  install -m644 nscd/nscd.tmpfiles "$pkgdir/data/data/com.termux/files/usr/glibc/lib/tmpfiles.d/nscd.conf"
  install -dm755 "$pkgdir/data/data/com.termux/files/usr/glibc/var/db/nscd"

  install -m644 posix/gai.conf "$pkgdir"/data/data/com.termux/files/usr/glibc/etc/gai.conf

  install -m755 "$srcdir/locale-gen" "$pkgdir/data/data/com.termux/files/usr/glibc/bin"

  # Create /etc/locale.gen
  install -m644 "$srcdir/locale.gen.txt" "$pkgdir/data/data/com.termux/files/usr/glibc/etc/locale.gen"
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
    "$srcdir/glibc/localedata/SUPPORTED" >> "$pkgdir/data/data/com.termux/files/usr/glibc/etc/locale.gen"

  # install C.UTF-8 so that it is always available
  install -dm755 "$pkgdir/data/data/com.termux/files/usr/glibc/lib/locale"
  cp -r "$srcdir/C.UTF-8" -t "$pkgdir/data/data/com.termux/files/usr/glibc/lib/locale"
  sed -i '/#C\.UTF-8 /d' "$pkgdir/data/data/com.termux/files/usr/glibc/etc/locale.gen"

  # Provide tracing probes to libstdc++ for exceptions, possibly for other
  # libraries too. Useful for gdb's catch command.
  install -Dm644 "$srcdir/sdt.h" "$pkgdir/data/data/com.termux/files/usr/glibc/include/sys/sdt.h"
  install -Dm644 "$srcdir/sdt-config.h" "$pkgdir/data/data/com.termux/files/usr/glibc/include/sys/sdt-config.h"

  sed -i 's|!/bin/bash|!/data/data/com.termux/files/usr/bin/bash|' "$pkgdir"/data/data/com.termux/files/usr/glibc/bin/*
  mkdir -p "$pkgdir"/data/data/com.termux/files/usr/bin
  install -m755 "$srcdir/grun" "$pkgdir/data/data/com.termux/files/usr/bin"
}
