# The original PKGBUILD was taken from the archlinux/svntogit-packages repo:
# - https://github.com/archlinux/svntogit-packages/blob/packages/libidn2/trunk/PKGBUILD

pkgname=libidn2-glibc
pkgver=2.3.4
pkgrel=0
pkgdesc="Free software implementation of IDNA2008, Punycode and TR46"
arch=(any)
url="https://www.gnu.org/software/libidn/#libidn2"
license=('GPL2' 'LGPL3')
makedepends=('ruby-ronn')
source=(https://ftp.gnu.org/gnu/libidn/libidn2-$pkgver.tar.gz)
sha256sums=('93caba72b4e051d1f8d4f5a076ab63c99b77faee019b72b9783b267986dbb45f')
groups=('gpkg-dev')

build() {
  cd libidn2-$pkgver

  case "$CARCH" in
    "aarch64")
      CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-aarch64.so.1"
      CXXFLAGS="${CFLAGS}"
      ;;
    "armv7h")
      CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-armhf.so.3"
      CXXFLAGS="${CFLAGS}"
      ;;
    "x86_64")
      CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-x86-64.so.2"
      CXXFLAGS="${CFLAGS}"
      ;;
    "i686")
      CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux.so.2"
      CXXFLAGS="${CFLAGS}"
      ;;
  esac

  ./configure \
    --prefix=/data/data/com.termux/files/usr/glibc \
    --disable-static
  make
}

package() {
  cd libidn2-$pkgver
  make DESTDIR="$pkgdir" install
  rm -f "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/info/dir
}

# vim:set ts=2 sw=2 et:
