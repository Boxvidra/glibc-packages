# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=tcl-glibc
pkgver=8.6.12
pkgrel=0
pkgdesc="Powerful, easy-to-learn dynamic programming language"
arch=('aarch64' 'arm' 'x86_64' 'i686')
url="http://tcl.sourceforge.net/"
license=('custom')
depends=('zlib-glibc')
source=(https://downloads.sourceforge.net/sourceforge/tcl/tcl${pkgver}-src.tar.gz)
sha256sums=('26c995dd0f167e48b11961d891ee555f680c175f7173ff8cb829f4ebcde4c1a6')

prepare() {
  cd tcl${pkgver}
  # we build the tcl sqlite interface in sqlite-tcl package
  rm -rf pkgs/sqlite3*
}

build() {
  cd tcl${pkgver}/unix
  ./configure --prefix=${prefix}/glibc --mandir=${prefix}/glibc/share/man --enable-threads --enable-64bit
  make
}

package() {
  cd tcl${pkgver}/unix
  make INSTALL_ROOT="${pkgdir}" install install-private-headers
  ln -sf tclsh${pkgver%.*} "${terdir}/usr/glibc/bin/tclsh"
  ln -sf libtcl${pkgver%.*}.so "${terdir}/usr/glibc/lib/libtcl.so"
  install -Dm644 ../license.terms "${terdir}/usr/glibc/share/licenses/${pkgname}/LICENSE"
  install -Dm644 tcl.m4 -t "$terdir"/usr/glibc/share/aclocal
  chmod 644 "$terdir/usr/glibc/lib/libtclstub8.6.a"

  # remove buildroot traces
  _tclver=8.6
  sed -e "s#${srcdir}/tcl${pkgver}/unix#${prefix}/glibc/lib#" \
      -e "s#${srcdir}/tcl${pkgver}#${prefix}/glibc/include#" \
      -e "s#'{/usr/lib} '#'${prefix}/glibc/lib/tcl$_tclver'#" \
      -i "${terdir}/usr/glibc/lib/tclConfig.sh"

  tdbcver=tdbc1.1.3
  sed -e "s#${srcdir}/tcl${pkgver}/unix/pkgs/$tdbcver#${prefix}/glibc/lib/$tdbcver#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/$tdbcver/generic#${prefix}/glibc/include#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/$tdbcver/library#${prefix}/glibc/lib/tcl${pkgver%.*}#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/$tdbcver#${prefix}/glibc/include#" \
      -i "${terdir}/usr/glibc/lib/$tdbcver/tdbcConfig.sh"

  itclver=itcl4.2.2
  sed -e "s#${srcdir}/tcl${pkgver}/unix/pkgs/$itclver#${prefix}/glibc/lib/$itclver#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/$itclver/generic#${prefix}/glibc/include#" \
      -e "s#${srcdir}/tcl${pkgver}/pkgs/$itclver#${prefix}/glibc/include#" \
      -i "${terdir}/usr/glibc/lib/$itclver/itclConfig.sh"
}
