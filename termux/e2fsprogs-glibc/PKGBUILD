# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgbase=e2fsprogs
pkgname=('e2fsprogs-glibc' 'fuse2fs-glibc')
pkgver=1.46.5
pkgrel=0
pkgdesc='Ext2/3/4 filesystem utilities'
arch=('aarch64' 'arm' 'x86_64' 'i686')
license=('GPL' 'LGPL' 'MIT')
url='http://e2fsprogs.sourceforge.net'
options=('debug')
makedepends=('util-linux' 'fuse2-glibc')
validpgpkeys=('3AB057B7E78D945C8C5591FBD36F769BC11804F0') # Theodore Ts'o <tytso@mit.edu>
source=("https://www.kernel.org/pub/linux/kernel/people/tytso/${pkgbase}/v${pkgver}/${pkgbase}-${pkgver}.tar."{xz,sign}
        'MIT-LICENSE')
sha256sums=('2f16c9176704cf645dc69d5b15ff704ae722d665df38b2ed3cfc249757d8d81e'
            'SKIP'
            'cc45386c1d71f438ad648fd7971e49e3074ad9dbacf9dd3a5b4cb61fd294ecbb')


prepare() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  # Remove unnecessary init.d directory
  sed -i '/init\.d/s|^|#|' misc/Makefile.in
}

build() {
  cd "${srcdir}/${pkgbase}-${pkgver}"

  ./configure \
      --prefix=${prefix}/glibc \
      --with-root-prefix='' \
      --libdir=${prefix}/glibc/lib \
      --sbindir=${prefix}/glibc/bin \
      --enable-elf-shlibs \
      --disable-fsck \
      --disable-uuidd \
      --disable-libuuid \
      --disable-libblkid
  make LN='cp'

  # regenerate locale files
  find po/ -name '*.gmo' -delete
  make -C po update-gmo
}

package_e2fsprogs-glibc() {
  optdepends=('lvm2: for e2scrub'
              'util-linux: for e2scrub'
              'smtp-forwarder: for e2scrub_fail script')
  provides=('libcom_err.so'
            'libe2p.so'
            'libext2fs.so'
            'libss.so')
  backup=('data/data/com.termux/files/usr/glibc/etc/mke2fs.conf'
          'data/data/com.termux/files/usr/glibc/etc/e2scrub.conf')

  unset MAKEFLAGS

  cd "${srcdir}/${pkgbase}-${pkgver}"

  make DESTDIR="${pkgdir}" LN='cp' install install-libs

  sed -i -e 's/^AWK=.*/AWK=awk/' "${terdir}/usr/glibc/bin/compile_et"

  # remove references to build directory
  sed -i -e 's#^DIR=.*#DIR="${prefix}/glibc/share/ss"#' "${terdir}/usr/glibc/bin/mk_cmds"
  sed -i -e 's#^DIR=.*#DIR="${prefix}/glibc/share/et"#' "${terdir}/usr/glibc/bin/compile_et"

  # remove static libraries with a shared counterpart
  rm "${terdir}"/usr/glibc/lib/lib{com_err,e2p,ext2fs,ss}.a

  # remove fuse2fs which will be packaged separately
  rm "${terdir}"/usr/glibc/{bin/fuse2fs,share/man/man1/fuse2fs.1}

  # install MIT license
  install -D -m0644 "${srcdir}/MIT-LICENSE" \
    "${terdir}/usr/glibc/share/licenses/e2fsprogs/MIT-LICENSE"

  rm -fr "${pkgdir}"/etc
}

package_fuse2fs-glibc() {
  pkgdesc='Ext2/3/4 filesystem driver for FUSE'

  cd "${srcdir}/${pkgbase}-${pkgver}"

  install -D -m0755 'misc/fuse2fs' "${terdir}/usr/glibc/bin/fuse2fs"
  install -D -m0644 'misc/fuse2fs.1' "${terdir}/usr/glibc/share/man/man1/fuse2fs.1"
  mkdir -p "${terdir}"/usr/glibc/share/licenses
  ln -s "${pkgbase}" "${terdir}/usr/glibc/share/licenses/fuse2fs"
}
