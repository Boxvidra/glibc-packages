# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Contributor: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=libuv-glibc
pkgver=1.43.0
pkgrel=0
pkgdesc="Multi-platform support library with a focus on asynchronous I/O"
arch=('aarch64' 'arm' 'x86_64' 'i686')
url="https://github.com/libuv/libuv"
license=('custom')
source=(https://dist.libuv.org/dist/v$pkgver/libuv-v$pkgver.tar.gz{,.sign})
sha512sums=('ba48140ac5733e09462a731fe9194c902b011a1a93a63175949c616367bde56bd1c114e8487c5580bee2d4656b45ab8cdc4fc335f67dd35e793c1a943f69fb34'
            'SKIP')
# PGP key IDs are available from https://github.com/libuv/libuv/blob/v1.x/MAINTAINERS.md
validpgpkeys=('57353E0DBDAAA7E839B66A1AFF47D5E4AD8B4FDC'  # Colin Ihrig (@cjihrig)
              'AEAD0A4B686767751A0E4AEF34A25FB128246514') # Jameson Nash (@vtjnash)

build() {
    cd libuv-v$pkgver
    ./autogen.sh
    ./configure --prefix=${prefix}/glibc
    make
    make man -C docs
}

package() {
    cd libuv-v$pkgver

    make DESTDIR="$pkgdir" install

    install -Dm644 LICENSE \
        "$terdir"/usr/glibc/share/licenses/libuv/LICENSE
    install -Dm644 AUTHORS \
        "$terdir"/usr/glibc/share/doc/libuv/AUTHORS
    install -Dm644 README.md \
        "$terdir"/usr/glibc/share/doc/libuv/README.md
    install -Dm644 ChangeLog \
        "$terdir"/usr/glibc/share/doc/libuv/ChangeLog
    install -Dm644 docs/build/man/libuv.1 \
        "$terdir"/usr/glibc/share/man/man1/libuv.1
}
