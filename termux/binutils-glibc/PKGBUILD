# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>

# toolchain build order: linux-api-headers->glibc->binutils->gcc->glibc->binutils->gcc

pkgname=binutils-glibc
pkgver=2.38
pkgrel=0
pkgdesc='A set of programs to assemble and manipulate binary and object files'
arch=(any)
url='https://www.gnu.org/software/binutils/'
license=('GPL3' 'GPL' 'FDL1.3' 'custom:FSFAP')
#_commit=cb5f6a3e146cc70bc2d864989386df80acec5d3e
#source=(git+https://sourceware.org/git/binutils-gdb.git#commit=$_commit)
source=(https://ftp.gnu.org/gnu/binutils/binutils-$pkgver.tar.xz{,.sig}
        gold-warn-unsupported.patch
        fix-incorrect-undefined-symbol.patch)
sha256sums=('e316477a914f567eccc34d5d29785b8b0f5a10208d36bbacedcc39048ecfe024'
            'SKIP'
            '2d430b66f84a19c154725ec535280c493be1d34f1a90f95208dce02fecd0e4e4'
            'fd33b2f8cac7561cecf3fdbb5a50fd2f2dfa6420516cbe57c47784a06fa16bf6')
validpgpkeys=(3A24BC1E8FB409FA9F14371813FCEF89DD9E3C4F)

prepare() {
  [[ ! -d binutils-gdb ]] && ln -s binutils-$pkgver binutils-gdb
  mkdir -p binutils-build

  cd binutils-gdb

  # Turn off development mode (-Werror, gas run-time checks, date in sonames)
  sed -i '/^development=/s/true/false/' bfd/development.sh

  # hack! - libiberty configure tests for header files using "$CPP $CPPFLAGS"
  sed -i "/ac_cpp=/s/\$CPPFLAGS/\$CPPFLAGS -O2/" libiberty/configure

  # Creds @Fedora
  # Change the gold configuration script to only warn about
  # unsupported targets.  This allows the binutils to be built with
  # BPF support enabled.
  /data/data/com.termux/files/usr/bin/patch -Np1 -i "${srcdir}"/gold-warn-unsupported.patch

  # fix incorrect "undefined reference" errors
  # https://sourceware.org/bugzilla/show_bug.cgi?id=28879
  /data/data/com.termux/files/usr/bin/patch -Np1 -i "${srcdir}"/fix-incorrect-undefined-symbol.patch
}

build() {
  cd binutils-build

  CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-aarch64.so.1"
  CXXFLAGS="${CFLAGS}"

  "$srcdir/binutils-gdb/configure" \
    --prefix=/data/data/com.termux/files/usr/glibc \
    --with-lib-path=/data/data/com.termux/files/usr/glibc/lib:/data/data/com.termux/files/usr/glibc/local/lib \
    --with-bugurl=https://bugs.archlinux.org/ \
    --enable-cet \
    --enable-deterministic-archives \
    --enable-gold \
    --enable-install-libiberty \
    --enable-ld=default \
    --enable-lto \
    --enable-pgo-build=lto \
    --enable-plugins \
    --enable-relro \
    --enable-shared \
    --enable-threads \
    --disable-gdb \
    --disable-gdbserver \
    --disable-libdecnumber \
    --disable-readline \
    --disable-sim \
    --disable-werror \
    --with-debuginfod \
    --with-pic \
    --with-system-zlib

  make -O tooldir=/data/data/com.termux/files/usr/glibc

  # Extract the FSF All Permissive License
  # <https://www.gnu.org/prep/maintain/html_node/License-Notices-for-Other-Files.html>
  # used for some linker scripts.
  tail -n 5 "${srcdir}"/binutils-${pkgver}/ld/scripttempl/README > FSFAP
}

package() {
  cd binutils-build
  make prefix="$pkgdir/data/data/com.termux/files/usr/glibc" tooldir="$pkgdir/data/data/com.termux/files/usr/glibc" install

  # Remove unwanted files
  rm -f "$pkgdir"/data/data/com.termux/files/usr/glibc/share/man/man1/{dlltool,nlmconv,windres,windmc}*

  # No shared linking to these files outside binutils
  rm -f "$pkgdir"/data/data/com.termux/files/usr/glibc/lib/lib{bfd,opcodes}.so
  echo 'INPUT( /data/data/com.termux/files/usr/glibc/lib/libbfd.a -liberty -lz -ldl )' > "$pkgdir/data/data/com.termux/files/usr/glibc/lib/libbfd.so"
  echo 'INPUT( /data/data/com.termux/files/usr/glibc/lib/libopcodes.a -lbfd )' > "$pkgdir/data/data/com.termux/files/usr/glibc/lib/libopcodes.so"

  # install FSF All Permissive License
  install -Dm644 -t "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/licenses/${pkgname}/ FSFAP

  rm -f "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/info/dir
}
