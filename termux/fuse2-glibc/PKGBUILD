# Maintainer: Ronald van Haren <ronald.archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: Mark Rosenstand <mark@archlinux.org>

pkgname=fuse2-glibc
pkgver=2.9.9
pkgrel=0
pkgdesc="A library that makes it possible to implement a filesystem in a userspace program."
arch=('aarch64' 'arm' 'x86_64' 'i686')
url='https://github.com/libfuse/libfuse'
license=('GPL2')
provides=(fuse=$pkgver) # TODO: remove it once all packages start to use 'fuse2' name
depends=('glibc')
makedepends=('pkgconf-glibc')
options=(!emptydirs)
source=(https://github.com/libfuse/libfuse/releases/download/fuse-$pkgver/fuse-$pkgver.tar.gz{,.asc}
        fuse_kernel.h.patch
        ulockmgr_server.c.patch)
sha1sums=('943ba651b14bc4a3c6fd959ed4b8c04f4a59032d'
          'SKIP'
          'SKIP'
          'SKIP')
validpgpkeys=(ED31791B2C5C1613AF388B8AD113FCAC3C4E599F) # Nikolaus Rath <Nikolaus@rath.org>

prepare() {
  for i in *.patch; do
    patch -Np1 -i ${srcdir}/${i}
  done
}

build() {
  cd fuse-$pkgver

  UDEV_RULES_PATH=${prefix}/glibc/lib/udev/rules.d MOUNT_FUSE_PATH=${prefix}/glibc/bin ./configure --prefix=${prefix}/glibc --libdir=${prefix}/glibc/lib --enable-lib --enable-util --disable-example
  make
}

package() {
  cd fuse-$pkgver

  make DESTDIR="${pkgdir}" install

  # Remove init script in wrong path
  # Don't add our own for now, as fusectl fs oopses on 2.6.18
  rm -r "${pkgdir}"/etc

  # static device nodes are handled by udev
  rm -r "${pkgdir}"/dev
}
