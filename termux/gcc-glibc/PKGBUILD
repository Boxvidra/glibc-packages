# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Christer Solskogen <christer.solskogen@gmail.com>
# Build order: aarch64-binutils -> aarch64-linux-api-headers -> aarch64-gcc-bootstrap -> aarch64-glibc -> aarch64-gcc -> aarch64-glibc (again)

_arch=aarch64
_target=$_arch-unknown-linux-gnu
pkgname=(gcc-glibc gcc-libs)
pkgver=12.1.0
_islver=0.24
pkgrel=0
#_snapshot=8-20190111
pkgdesc='The GNU Compiler Collection'
arch=('aarch64' 'arm' 'x86_64' 'i686')
url='https://gcc.gnu.org/'
makedepends=(
  binutils-glibc
  gcc-glibc
  git
  libmpc-glibc
  libxcrypt-glibc
  python
  zstd-glibc
)
license=(GPL LGPL FDL)
replaces=($_arch-gcc-bootstrap)
options=(!emptydirs !strip staticlibs)
source=(https://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz{,.sig}
        #https://gcc.gnu.org/pub/gcc/snapshots/$_snapshot/gcc-$_snapshot.tar.xz
	"https://libisl.sourceforge.io/isl-${_islver}.tar.xz"
        c89 c99
        aarch64-linux.h.patch
        collect2.cc.patch
        gcc.cc.patch)
_libdir=data/data/com.termux/files/usr/glibc/lib/gcc/$_target/${pkgver%%+*}
sha256sums=('62fd634889f31c02b64af2c468f064b47ad1ca78411c45abe6ac4b5f8dd19c7b'
            'SKIP'
            '043105cc544f416b48736fff8caf077fb0663a717d06b1113f16e391ac99ebad'
            'd87254ee2c816aee66b00dd5002966cf3fe9aca4969eaf160c47150583357790'
            '9ad6d475acf0b19ca0e420f32867c6a5186ffbae50481ec64a140165f8988b60'
            '0cbb7e034ac5e6d196ce04550ce6a4183553930b892d3d62b43c7981120e250d'
            '40f1d9209723eb9bbfd722d41af775aa3c6af4225b04cb10fbef24dde1a4d88d'
            '9ce32ae0cd3bffafa4de69f3c44e657629d192b6c826c10bd9b6cb15f9693770')
validpgpkeys=(F3691687D867B81B51CE07D9BBE43771487328A9  # bpiotrowski@archlinux.org
              86CFFCA918CF3AF47147588051E8B148A9999C34  # evangelos@foutrelis.com
              13975A70E63C361C73AE69EF6EEB81F8981C74C7  # richard.guenther@gmail.com
              D3A93CAD751C2AF4F8C7AD516C35B99309B5FA62) # Jakub Jelinek <jakub@redhat.com>

if [ -n "$_snapshot" ]; then
  _basedir=gcc-$_snapshot
else
  _basedir=gcc-$pkgver
fi

prepare() {
  for i in *.patch; do
    patch -Np1 -i ${srcdir}/$i
  done

  cd $_basedir

  # link isl for in-tree builds
  ln -sf ../isl-$_islver isl

  sed -i 's|mabi.lp64=../lib64|mabi.lp64=../lib|' gcc/config/aarch64/t-aarch64-linux

  mkdir -p "$srcdir"/gcc-build
}

build() {
  cd gcc-build

  "$srcdir"/$_basedir/configure \
      --prefix=${prefix}/glibc \
      --libdir=${prefix}/glibc/lib \
      --with-native-system-header-dir=${prefix}/glibc/include \
      --target=$_target \
      --disable-nls --enable-default-pie \
      --enable-languages=c,c++,fortran \
      --with-system-zlib --enable-__cxa_atexit \
      --enable-linker-build-id --enable-plugin \
      --with-linker-hash-style=gnu --enable-gnu-indirect-function \
      --disable-multilib --disable-werror \
      --enable-checking=release --disable-static \
      --with-arch=armv8-a \
      --enable-fix-cortex-a53-835769 --enable-fix-cortex-a53-843419 \
      --enable-host-shared --disable-libssp --disable-libstdcxx-pch \
      --without-zstd --disable-bootstrap

  make -O
}

package_gcc-libs() {
  pkgdesc='Runtime libraries shipped by GCC'
  depends=('glibc>=2.27')
  options=(!emptydirs !strip)

  cd gcc-build
  make -C $_target/libgcc DESTDIR="$pkgdir" install-shared
  rm -f "$pkgdir/$_libdir/libgcc_eh.a"

  for lib in libatomic \
             libgfortran \
             libgomp \
             libitm \
             libquadmath \
             libsanitizer/{a,l,ub,t}san \
             libstdc++-v3/src \
             libvtv; do
    make -C $_target/$lib DESTDIR="$pkgdir" install-toolexeclibLTLIBRARIES
  done

  make -C $_target/libstdc++-v3/po DESTDIR="$pkgdir" install

  for lib in libgomp \
             libitm \
             libquadmath; do
    make -C $_target/$lib DESTDIR="$pkgdir" install-info
  done

  # Install Runtime Library Exception
  install -Dm644 "$srcdir/gcc-${pkgver}/COPYING.RUNTIME" \
    "$terdir/usr/glibc/share/licenses/gcc-libs/RUNTIME.LIBRARY.EXCEPTION"

  rm -f "${terdir}"/usr/glibc/share/info/dir
}

package_gcc-glibc() {
  pkgdesc="The GNU Compiler Collection - C and C++ frontends"
  depends=("gcc-libs=$pkgver-$pkgrel" 'binutils-glibc>=2.28' libmpc-glibc zstd-glibc)
  options=(!emptydirs)

  cd gcc-build

  make -C gcc DESTDIR="$pkgdir" install-driver install-cpp install-gcc-ar \
    c++.install-common install-headers install-plugin install-lto-wrapper

  install -m755 -t "$terdir/usr/glibc/bin/" gcc/gcov{,-tool}
  install -m755 -t "$pkgdir/${_libdir}/" gcc/{cc1,cc1plus,collect2,lto1}

  make -C $_target/libgcc DESTDIR="$pkgdir" install
  rm -f "$terdir"/usr/glibc/lib{,32}/libgcc_s.so*

  make -C $_target/libstdc++-v3/src DESTDIR="$pkgdir" install
  make -C $_target/libstdc++-v3/include DESTDIR="$pkgdir" install
  make -C $_target/libstdc++-v3/libsupc++ DESTDIR="$pkgdir" install
  make -C $_target/libstdc++-v3/python DESTDIR="$pkgdir" install

  make DESTDIR="$pkgdir" install-libcc1
  install -d "$terdir/usr/glibc/share/gdb/auto-load/usr/lib"
  mv "$terdir"/usr/glibc/lib/libstdc++.so.6.*-gdb.py \
    "$terdir/usr/glibc/share/gdb/auto-load/usr/lib/"
  rm "$terdir"/usr/glibc/lib/libstdc++.so*
  rm "$terdir"/usr/glibc/lib/libstdc++.la

  make DESTDIR="$pkgdir" install-fixincludes
  make -C gcc DESTDIR="$pkgdir" install-mkheaders

  make -C lto-plugin DESTDIR="$pkgdir" install
  install -dm755 "$terdir"/usr/glibc/lib/bfd-plugins/
  ln -s /${_libdir}/liblto_plugin.so \
    "$terdir/usr/glibc/lib/bfd-plugins/"

  make -C $_target/libgomp DESTDIR="$pkgdir" install-nodist_{libsubinclude,toolexeclib}HEADERS
  make -C $_target/libitm DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS
  make -C $_target/libquadmath DESTDIR="$pkgdir" install-nodist_libsubincludeHEADERS
  make -C $_target/libsanitizer DESTDIR="$pkgdir" install-nodist_{saninclude,toolexeclib}HEADERS
  make -C $_target/libsanitizer/asan DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS
  make -C $_target/libsanitizer/tsan DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS
  make -C $_target/libsanitizer/lsan DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS

  make -C gcc DESTDIR="$pkgdir" install-man install-info
  rm "$terdir"/usr/glibc/share/man/man1/gfortran.1
  rm "$terdir"/usr/glibc/share/info/gfortran.info

  make -C libcpp DESTDIR="$pkgdir" install
  make -C gcc DESTDIR="$pkgdir" install-po

  # many packages expect this symlink
  ln -s gcc "$terdir"/usr/glibc/bin/cc

  # POSIX conformance launcher scripts for c89 and c99
  install -Dm755 "$srcdir/c89" "$terdir/usr/glibc/bin/c89"
  install -Dm755 "$srcdir/c99" "$terdir/usr/glibc/bin/c99"

  # byte-compile python libraries
  python -m compileall "$terdir/usr/glibc/share/gcc-${pkgver%%+*}/"
  python -O -m compileall "$terdir/usr/glibc/share/gcc-${pkgver%%+*}/"

  # Install Runtime Library Exception
  install -d "$terdir/usr/glibc/share/licenses/$pkgname/"
  ln -s /data/data/com.termux/files/usr/glibc/share/licenses/gcc-libs/RUNTIME.LIBRARY.EXCEPTION \
    "$terdir/usr/glibc/share/licenses/$pkgname/"

  rm -f "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/info/dir
}
