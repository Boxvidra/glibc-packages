# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Andrzej Giniewicz <gginiu@gmail.com>
# Contributor: Johan Förberg <johan@forberg.se>

pkgname=zstd-glibc
pkgver=1.5.2
pkgrel=2
pkgdesc='Zstandard - Fast real-time compression algorithm'
url='https://facebook.github.io/zstd/'
arch=(any)
license=(BSD GPL2)
depends=()
makedepends=(gtest)
options=(debug)
source=(https://github.com/facebook/zstd/releases/download/v${pkgver}/zstd-${pkgver}.tar.zst{,.sig})
sha256sums=('3ea06164971edec7caa2045a1932d757c1815858e4c2b68c7ef812647535c23f'
            'SKIP')
b2sums=('513e4526a92bcb59416b3457d186a30e554f9e0cf21d7114eb3e9fbcbd9d662c8d95cf0b06237f6fe3f756862c63de0aa146d6a23cb4111c16e6459608d115f1'
        'SKIP')
validpgpkeys=(4EF4AC63455FC9F4545D9B7DEF8FE99528B52FFD)

build() {
  cd zstd-${pkgver}
  make
  make -C contrib/pzstd
}

package() {
  cd zstd-${pkgver}
  make PREFIX=/data/data/com.termux/files/usr/glibc DESTDIR="${pkgdir}" install
  ln -sf /data/data/com.termux/files/usr/glibc/bin/zstd "${pkgdir}/data/data/com.termux/files/usr/glibc/bin/zstdmt"
  install -Dm 755 contrib/pzstd/pzstd -t "${pkgdir}/data/data/com.termux/files/usr/glibc/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/data/data/com.termux/files/usr/glibc/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
