# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: Judd Vinet <jvinet@zeroflux.org>

pkgname=expat-glibc
pkgver=2.4.8
pkgrel=0
pkgdesc="An XML parser library"
arch=('aarch64' 'arm' 'x86_64' 'i686')
url="https://libexpat.github.io/"
license=(MIT)
depends=(glibc)
makedepends=(cmake-glibc)
provides=(libexpat.so)
options=(debug)
source=("https://github.com/libexpat/libexpat/releases/download/R_${pkgver//./_}/expat-${pkgver}.tar.bz2"{,.asc})
sha512sums=('46cc9d725f359b77681a2875bfefa15ceee50eb9513f6577607c0c5833dfa4241565c74f26b84b38d802c3cd8c32f00204fd74272bcecbd21229425764eef86c'
            'SKIP')
b2sums=('a829bd94d2cc9b305874ae2539dd4388ea99eaed21d2da17bbd99a1e7735b0f7cb910d096f72c71b128dcb8ffdff9021109e7ff5e562d8cefad228fd8d66f897'
        'SKIP')
validpgpkeys=(3176EF7DB2367F1FCA4F306B1F9B0E909AF37285) # Sebastian Pipping

build() {
  cmake -DCMAKE_INSTALL_PREFIX=${prefix}/glibc \
        -DCMAKE_INSTALL_LIBDIR=lib \
        -DCMAKE_BUILD_TYPE=None \
        -W no-dev \
        -B build \
        -S "expat-$pkgver"
  make VERBOSE=1 -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" -C build install
  install -vDm 644 "expat-$pkgver/COPYING" -t "${terdir}/usr/glibc/share/licenses/$pkgname/"
  install -vDm 644 "expat-$pkgver/"{AUTHORS,Changes,README.md} -t "${terdir}/usr/glibc/share/doc/${pkgname}/"
}
