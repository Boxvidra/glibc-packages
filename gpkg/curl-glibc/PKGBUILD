# Maintainer: Dave Reisner <dreisner@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Lucien Immink <l.immink@student.fnt.hvu.nl>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgbase=curl
pkgname=(curl-glibc libcurl-compat-glibc)
pkgver=7.83.1
pkgrel=0
pkgdesc='An URL retrieval utility and library'
arch=('aarch64' 'arm' 'x86_64' 'i686')
url='https://curl.haxx.se'
license=('MIT')
depends=('brotli-glibc' 'krb5-glibc' 'libidn2-glibc'
         'libnghttp2-glibc' 'libpsl-glibc' 'libssh2-glibc'
         'openssl-glibc' 'zlib-glibc' 'zstd-glibc')
source=("https://curl.haxx.se/download/curl-${pkgver}.tar.gz"{,.asc})
sha512sums=('0b909b7ed55f9a9789584fd9d2033d5838dcf29e33adf6657258e97ebe7c91b26282007687a729c8385594fd8220bd718fd008154926b87b8da254d586fab3c7'
            'SKIP')
validpgpkeys=('27EDEAF22F3ABCEB50DB9A125CC908FDB71E12C2') # Daniel Stenberg

_configure_options=(
  --prefix=/data/data/com.termux/files/usr/glibc
  --mandir=/data/data/com.termux/files/usr/glibc/share/man
  --disable-ldap
  --disable-ldaps
  --disable-manual
  --enable-ipv6
  --enable-threaded-resolver
  --with-libssh2
  --with-openssl
  --with-random='/dev/urandom'
  --with-ca-bundle=/data/data/com.termux/files/usr/glibc/etc/ssl/certs/ca-certificates.crt
)

build() {
  mkdir build-curl{,-compat} || true

  # build curl
  cd "${srcdir}"/build-curl

  "${srcdir}/${pkgbase}-${pkgver}"/configure \
    "${_configure_options[@]}" \
    --enable-versioned-symbols
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make

  # build libcurl-compat
  cd "${srcdir}"/build-curl-compat

  "${srcdir}/${pkgbase}-${pkgver}"/configure \
    "${_configure_options[@]}" \
    --disable-versioned-symbols
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0/g' libtool
  make -C lib
}

package_curl-glibc() {
  cd build-curl

  make DESTDIR="${pkgdir}" install
  make DESTDIR="${pkgdir}" install -C scripts

  cd "${srcdir}/curl-${pkgver}"

  # license
  install -Dt "${terdir}/usr/glibc/share/licenses/curl" -m0644 COPYING
}

package_libcurl-compat-glibc() {
  pkgdesc='An URL retrieval library (without versioned symbols)'
  depends=('curl-glibc' 'openssl-glibc')

  cd "${srcdir}"/build-curl-compat

  make -C lib DESTDIR="${pkgdir}" install

  mv "${terdir}"/usr/glibc/lib/libcurl{,-compat}.so.4.8.0
  rm "${terdir}"/usr/glibc/lib/libcurl.{a,so,la}*
  for version in 3 4.0.0 4.1.0 4.2.0 4.3.0 4.4.0 4.5.0 4.6.0; do
    ln -s libcurl-compat.so.4.7.0 "${terdir}"/usr/glibc/lib/libcurl.so.${version}
  done

  install -dm 0755 "${terdir}"/usr/glibc/share/licenses
  ln -s curl "${terdir}"/usr/glibc/share/licenses/libcurl-compat
}
