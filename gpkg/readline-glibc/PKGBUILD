# Maintainer: Giancarlo Razzolini <grazzolini@archlinux.org>
# Contributor:  Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Allan McRae <allan@archlinux.org>
# Contributor: judd <jvinet@zeroflux.org>

pkgname=readline-glibc
_basever=8.1
_patchlevel=002
pkgver=${_basever}.${_patchlevel}
pkgrel=0
pkgdesc='GNU readline library'
arch=('aarch64' 'arm' 'x86_64' 'i686')
url='https://tiswww.case.edu/php/chet/readline/rltop.html'
license=('GPL')
backup=('data/data/com.termux/files/usr/glibc/etc/inputrc')
depends=('glibc' 'ncurses-glibc' 'libncursesw.so')
provides=('libhistory.so' 'libreadline.so')
options=('!emptydirs' 'debug')
source=(https://ftp.gnu.org/gnu/readline/readline-$_basever.tar.gz{,.sig}
        inputrc)
validpgpkeys=('7C0135FB088AAF6C66C650B9BB5869F064EA74AB') # Chet Ramey

if [ $_patchlevel -gt 0 ]; then
    for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
        source=(${source[@]} https://ftp.gnu.org/gnu/readline/readline-$_basever-patches/readline${_basever//.}-$(printf "%03d" $_p){,.sig})
    done
fi

prepare() {
  cd readline-$_basever
  for (( _p=1; _p <= $((10#${_patchlevel})); _p++ )); do
    msg "applying patch readline${_basever//.}-$(printf "%03d" $_p)"
    patch -p0 -i ../readline${_basever//.}-$(printf "%03d" $_p)
  done

  # remove RPATH from shared objects (FS#14366)
  sed -i 's|-Wl,-rpath,$(libdir) ||g' support/shobj-conf
}

build() {
  cd readline-$_basever

  # build with -fPIC for x86_64 (FS#15634)
  [[ $CARCH == "x86_64" ]] && CFLAGS="$CFLAGS -fPIC"

  ./configure --prefix=${prefix}/glibc
  make SHLIB_LIBS=-lncurses
}

package() {
  make -C readline-$_basever DESTDIR="$pkgdir" install
  install -Dm644 inputrc "$terdir"/usr/glibc/etc/inputrc
  rm ${terdir}/usr/glibc/share/info/dir
}

md5sums=('e9557dd5b1409f5d7b37ef717c64518e'
         'SKIP'
         '58d54966c1191db45973cb3191ac621a'
         '8d127cb06ae797a05fba88ada117e6f7'
         'SKIP'
         '41cd79733ec880809f3cee0c2e8e2483'
         'SKIP')
