# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=keyutils-glibc
_tag='5678a1aae8834b5c16b5ed7dc72ef8836a29e122' # git rev-parse v${pkgver}
pkgver=1.6.3
pkgrel=1
pkgdesc='Linux Key Management Utilities'
arch=('aarch64' 'arm' 'x86_64' 'i686')
url='https://www.kernel.org/'
license=('GPL2' 'LGPL2.1')
depends=('glibc' 'bash')
makedepends=('git')
backup=('data/data/com.termux/files/usr/glibc/etc/request-key.conf')
validpgpkeys=('A86E54B0D5E1B4E0AB7C640FFBB7576BA7CB0B6B') # David Howells <dhowells@redhat.com>
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/dhowells/keyutils.git#tag=${_tag}?signed"
        'request-key.conf.patch' 'reproducible.patch')
sha256sums=('SKIP'
            '1ecf854011ce1a15e238653187fd6aed903b94c16d6a99bfca463bd8ca1c2f40'
            '7bb7400b2b8c8f0288c86ec9191f8964a1e682745a204013d5fc7c2e1a253d8e')

prepare() {
  cd keyutils
  # fix paths of binaries in /etc/request-key.conf
  patch -Np0 -i ../request-key.conf.patch

  # make keyutils reproducible
  patch -Np1 -i ../reproducible.patch
}

build() {
  cd keyutils
  make CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}" SBINDIR="${prefix}/glibc/bin" BINDIR="${prefix}/glibc/bin"
}

package() {
  cd keyutils
  mkdir -p "${terdir}/usr/glibc"
  make DESTDIR="${terdir}/usr/glibc" SBINDIR='/bin' BINDIR='/bin' LIBDIR='/lib' USRLIBDIR="/lib" INCLUDEDIR="/include" SHAREDIR="/share/keyutils" MANDIR="/share/man" install
  rm "${terdir}"/usr/glibc/lib/libkeyutils.so
  ln -s libkeyutils.so.1 "${terdir}"/usr/glibc/lib/libkeyutils.so
}
