# Maintainer: Anatol Pomozov <anatol.pomozov@gmail.com>
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Christer Solskogen <christer.solskogen@gmail.com>

pkgname=(gcc-glibc gcc-libs)
pkgver=12.2.0
pkgrel=0
pkgdesc='The GNU Compiler Collection'
arch=(any)
url='https://gcc.gnu.org/'
license=(GPL LGPL FDL)
depends=()
replaces=($_arch-gcc-bootstrap)
options=(!emptydirs !strip staticlibs)
source=(https://ftp.gnu.org/gnu/gcc/gcc-$pkgver/gcc-$pkgver.tar.xz{,.sig}
        c89 c99
        aarch64-linux.h.patch)
_libdir=data/data/com.termux/files/usr/glibc/lib/gcc/$CHOST/${pkgver%%+*}

sha256sums=('e549cf9cf3594a00e27b6589d4322d70e0720cdd213f39beb4181e06926230ff'
            'SKIP'
            'd87254ee2c816aee66b00dd5002966cf3fe9aca4969eaf160c47150583357790'
            '9ad6d475acf0b19ca0e420f32867c6a5186ffbae50481ec64a140165f8988b60'
            'SKIP')

validpgpkeys=(F3691687D867B81B51CE07D9BBE43771487328A9  # bpiotrowski@archlinux.org
              86CFFCA918CF3AF47147588051E8B148A9999C34  # evangelos@foutrelis.com
              13975A70E63C361C73AE69EF6EEB81F8981C74C7  # richard.guenther@gmail.com
              D3A93CAD751C2AF4F8C7AD516C35B99309B5FA62) # Jakub Jelinek <jakub@redhat.com>

_basedir=gcc-$pkgver

prepare() {
  for i in *.patch; do
    /data/data/com.termux/files/usr/bin/patch -Np1 -i ${srcdir}/$i
  done

  cd $_basedir

  mkdir -p "$srcdir"/gcc-build
}

build() {
  cd gcc-build

  CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-aarch64.so.1"
  CXXFLAGS="${CFLAGS}"

  "$srcdir"/$_basedir/configure \
      --prefix=/data/data/com.termux/files/usr/glibc \
      --libdir=/data/data/com.termux/files/usr/glibc/lib \
      --disable-nls --enable-default-pie \
      --enable-languages=c,c++,fortran \
      --with-system-zlib --enable-__cxa_atexit \
      --enable-linker-build-id --enable-plugin \
      --with-linker-hash-style=gnu --enable-gnu-indirect-function \
      --disable-multilib --disable-werror \
      --enable-checking=release --disable-static \
      --enable-fix-cortex-a53-835769 --enable-fix-cortex-a53-843419 \
      --enable-host-shared --disable-libssp --disable-libstdcxx-pch \
      --without-zstd --disable-bootstrap

  make -O BOOT_CFLAGS="$CFLAGS" \
      BOOT_CXXFLAGS="$CXXFLAGS"
}

package_gcc-libs() {
  pkgdesc='Runtime libraries shipped by GCC'
  options=(!emptydirs !strip)

  cd gcc-build
  make -C $CHOST/libgcc DESTDIR="$pkgdir" install-shared
  rm -f "$pkgdir/$_libdir/libgcc_eh.a"

  for lib in libatomic \
             libgfortran \
             libgomp \
             libitm \
             libquadmath \
             libsanitizer/{a,l,ub,t}san \
             libstdc++-v3/src \
             libvtv; do
    make -C $CHOST/$lib DESTDIR="$pkgdir" install-toolexeclibLTLIBRARIES
  done

  make -C $CHOST/libstdc++-v3/po DESTDIR="$pkgdir" install

  for lib in libgomp \
             libitm \
             libquadmath; do
    make -C $CHOST/$lib DESTDIR="$pkgdir" install-info
  done

  # Install Runtime Library Exception
  install -Dm644 "$srcdir/gcc-${pkgver}/COPYING.RUNTIME" \
    "$pkgdir/data/data/com.termux/files/usr/glibc/share/licenses/gcc-libs/RUNTIME.LIBRARY.EXCEPTION"

  rm -f "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/info/dir
}

package_gcc-glibc() {
  pkgdesc="The GNU Compiler Collection - C and C++ frontends"
  options=(!emptydirs)

  cd gcc-build

  make -C gcc DESTDIR="$pkgdir" install-driver install-cpp install-gcc-ar \
    c++.install-common install-headers install-plugin install-lto-wrapper

  install -m755 -t "$pkgdir/data/data/com.termux/files/usr/glibc/bin/" gcc/gcov{,-tool}
  install -m755 -t "$pkgdir/${_libdir}/" gcc/{cc1,cc1plus,collect2,lto1}

  make -C $CHOST/libgcc DESTDIR="$pkgdir" install
  rm -f "$pkgdir"/data/data/com.termux/files/usr/glibc/lib{,32}/libgcc_s.so*

  make -C $CHOST/libstdc++-v3/src DESTDIR="$pkgdir" install
  make -C $CHOST/libstdc++-v3/include DESTDIR="$pkgdir" install
  make -C $CHOST/libstdc++-v3/libsupc++ DESTDIR="$pkgdir" install
  make -C $CHOST/libstdc++-v3/python DESTDIR="$pkgdir" install

  make DESTDIR="$pkgdir" install-libcc1
  install -d "$pkgdir/data/data/com.termux/files/usr/glibc/share/gdb/auto-load/usr/lib"
  mv "$pkgdir"/data/data/com.termux/files/usr/glibc/lib/libstdc++.so.6.*-gdb.py \
    "$pkgdir/data/data/com.termux/files/usr/glibc/share/gdb/auto-load/usr/lib/"
  rm "$pkgdir"/data/data/com.termux/files/usr/glibc/lib/libstdc++.so*

  make DESTDIR="$pkgdir" install-fixincludes
  make -C gcc DESTDIR="$pkgdir" install-mkheaders

  make -C lto-plugin DESTDIR="$pkgdir" install
  install -dm755 "$pkgdir"/data/data/com.termux/files/usr/glibc/lib/bfd-plugins/
  ln -s /${_libdir}/liblto_plugin.so \
    "$pkgdir/data/data/com.termux/files/usr/glibc/lib/bfd-plugins/"

  make -C $CHOST/libgomp DESTDIR="$pkgdir" install-nodist_{libsubinclude,toolexeclib}HEADERS
  make -C $CHOST/libitm DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS
  make -C $CHOST/libquadmath DESTDIR="$pkgdir" install-nodist_libsubincludeHEADERS
  make -C $CHOST/libsanitizer DESTDIR="$pkgdir" install-nodist_{saninclude,toolexeclib}HEADERS
  make -C $CHOST/libsanitizer/asan DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS
  make -C $CHOST/libsanitizer/tsan DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS
  make -C $CHOST/libsanitizer/lsan DESTDIR="$pkgdir" install-nodist_toolexeclibHEADERS

  make -C gcc DESTDIR="$pkgdir" install-man install-info
  rm "$pkgdir"/data/data/com.termux/files/usr/glibc/share/man/man1/gfortran.1
  rm "$pkgdir"/data/data/com.termux/files/usr/glibc/share/info/gfortran.info

  make -C libcpp DESTDIR="$pkgdir" install
  make -C gcc DESTDIR="$pkgdir" install-po

  # many packages expect this symlink
  ln -s gcc "$pkgdir"/data/data/com.termux/files/usr/glibc/bin/cc

  # POSIX conformance launcher scripts for c89 and c99
  install -Dm755 "$srcdir/c89" "$pkgdir/data/data/com.termux/files/usr/glibc/bin/c89"
  install -Dm755 "$srcdir/c99" "$pkgdir/data/data/com.termux/files/usr/glibc/bin/c99"

  # byte-compile python libraries
  python -m compileall "$pkgdir/data/data/com.termux/files/usr/glibc/share/gcc-${pkgver%%+*}/"
  python -O -m compileall "$pkgdir/data/data/com.termux/files/usr/glibc/share/gcc-${pkgver%%+*}/"

  # Install Runtime Library Exception
  install -d "$pkgdir/data/data/com.termux/files/usr/glibc/share/licenses/$pkgname/"
  ln -s /data/data/com.termux/files/usr/glibc/share/licenses/gcc-libs/RUNTIME.LIBRARY.EXCEPTION \
    "$pkgdir/data/data/com.termux/files/usr/glibc/share/licenses/$pkgname/"

  rm -f "${pkgdir}"/data/data/com.termux/files/usr/glibc/share/info/dir
}
