# Maintainer: Anatol Pomozov
# Maintainer: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Christer Solskogen <christer.solskogen@gmail.com>
# Build order: aarch64-binutils -> aarch64-linux-api-headers -> aarch64-gcc-bootstrap -> aarch64-glibc -> aarch64-gcc -> aarch64-glibc (again)

_arch=aarch64
_target=$_arch-unknown-linux-gnu
pkgname=$_arch-glibc
pkgver=2.35
pkgrel=1
_commit=be176490b818b65b5162c332eb6b581690b16e5c
pkgdesc="GNU C Library ARM64 target"
arch=(any)
url='https://www.gnu.org/software/libc/'
license=('GPL' 'LGPL')
options=(!strip staticlibs)
source=(https://ftp.gnu.org/gnu/libc/glibc-$pkgver.tar.xz{,.sig}
	'_Fork.c.patch'
	'dl-tls_init_tp.c.patch'
	'pthread_create.c.patch'
        'faccessat.c.patch'
        'tst-rseq-disable.c.patch'
        'tst-rseq.h.patch'
        'rseq-internal.h.patch'
        'disable-clone3.patch'
        'locale-gen'
        'locale.gen.txt'
        'sdt-config.h'
        'sdt.h'
	'grun')
sha256sums=('5123732f6b67ccd319305efd399971d58592122bcc2a6518a1bd2510dd0cf52e'
            'SKIP'
            '0a25d0358ba67c9cad1d94707615d9a6249b5234a9de601a10f281a5c604cc83'
            '0cc8d30b73368bfb92f7ea8298e4a791bb8d641c97ec86ef1c1be6052920fe32'
            '69a74af5419df4b914190b29cedee762413716fdc397eee22d7dcb5a5efdb092'
            '55871505df12af8eea2e6d9d50d1337437d12f082f6fd2c673f9911cb9747938'
            '55365e5bf9d758ded46e82fd6165b6a1c7828ab96d9de18346653553fce3018d'
            '47a5eed52a0c91e86b7f26e5e716ce24b8eb545eee21dc17f98cafc1f4a2dbb4'
            'e4eb6b3a3b42b28e15010c7a062141f8fd262c85185a746639f4432594fe9a1d'
            'f35852b69cd3fc78f7e77b566075b24e7a641dd60484e05abfcb838294225d1a'
            'cdd916c0dc29e5d958ddef5c3530947244fd740ce7645601bfabae53512323b8'
            'd42648cea552ba5353a32e264686e992263289d5cc86207314dffc54ab514981'
            'cdc234959c6fdb43f000d3bb7d1080b0103f4080f5e67bcfe8ae1aaf477812f0'
            '774061aff612a377714a509918a9e0e0aafce708b87d2d7e06b1bd1f6542fe70'
            '8094c9c64280710a31ad8d66c9d691a2de6756f4b0ae7051d9a4f5f3b8a9dc30')
validpgpkeys=(7273542B39962DF7B299931416792B4EA25340F8  # "Carlos O'Donell <carlos@systemhalted.org>"
              BC7C7372637EC10C57D7AA6579C43DFBF1CF2187) # Siddhesh Poyarekar

prepare() {
  for i in *.patch; do
    /data/data/com.termux/files/usr/bin/patch -Np1 -i ${srcdir}/$i
  done

  mkdir -p glibc-build

  [[ ! -d glibc ]] && ln -s glibc-$pkgver glibc

  sed -i 's|/tmp|/data/data/com.termux/files/usr/tmp|g' $(grep -R /tmp/ glibc-$pkgver | grep -v /var/ | awk -F'[:=]' '{printf $1 "\n"}')
  sed -i 's|/var/tmp|/data/data/com.termux/files/usr/tmp|g' $(grep -R /var/tmp glibc-$pkgver | awk -F'[:=]' '{printf $1 "\n"}')
}

build() {
  cd glibc-build
  echo "build-programs=yes" > configparms
  echo "slibdir=/data/data/com.termux/files/usr/glibc/lib" >> configparms
  echo "rtlddir=/data/data/com.termux/files/usr/glibc/lib" >> configparms
  echo "sbindir=/data/data/com.termux/files/usr/glibc/bin" >> configparms
  echo "rootsbindir=/data/data/com.termux/files/usr/glibc/bin" >> configparms

  unset CFLAGS
  ../glibc-$pkgver/configure \
      --prefix=/data/data/com.termux/files/usr/glibc \
      --sysconfdir=/data/data/com.termux/files/usr/glibc/etc \
      --with-headers=/data/data/com.termux/files/usr/glibc/include \
      --host=$_target \
      --libdir=/data/data/com.termux/files/usr/glibc/lib \
      --with-bugurl=https://github.com/Maxython/glibc-for-termux/issues \
      --enable-kernel=4.4 \
      --enable-bind-now \
      --enable-stack-protector=strong \
      --enable-stackguard-randomization \
      --disable-timezone-tools \
      --enable-multi-arch CFLAGS="-O2 -pipe"

  make
}

package() {
  pkgdesc='GNU C Library'
  backup=(data/data/com.termux/files/usr/glibc/etc/gai.conf
          data/data/com.termux/files/usr/glibc/etc/locale.gen
          data/data/com.termux/files/usr/glibc/etc/nscd.conf)

  install -dm755 "$pkgdir/data/data/com.termux/files/usr/glibc/etc"
  touch "$pkgdir/data/data/com.termux/files/usr/glibc/etc/ld.so.conf"

  make -C glibc-build install_root="$pkgdir" install
  rm -f "$pkgdir"/data/data/com.termux/files/usr/glibc/etc/ld.so.{cache,conf}

  # Shipped in tzdata
  rm -f "$pkgdir"/usr/bin/{tzselect,zdump,zic}

  cd glibc

  install -dm755 "$pkgdir"/data/data/com.termux/files/usr/glibc/lib/{locale,systemd/system,tmpfiles.d}
  install -m644 nscd/nscd.conf "$pkgdir/data/data/com.termux/files/usr/glibc/etc/nscd.conf"
  install -m644 nscd/nscd.service "$pkgdir/data/data/com.termux/files/usr/glibc/lib/systemd/system"
  install -m644 nscd/nscd.tmpfiles "$pkgdir/data/data/com.termux/files/usr/glibc/lib/tmpfiles.d/nscd.conf"
  install -dm755 "$pkgdir/data/data/com.termux/files/usr/glibc/var/db/nscd"

  install -m644 posix/gai.conf "$pkgdir"/data/data/com.termux/files/usr/glibc/etc/gai.conf

  install -m755 "$srcdir/locale-gen" "$pkgdir/data/data/com.termux/files/usr/glibc/bin"

  # Create /etc/locale.gen
  install -m644 "$srcdir/locale.gen.txt" "$pkgdir/data/data/com.termux/files/usr/glibc/etc/locale.gen"
  sed -e '1,3d' -e 's|/| |g' -e 's|\\| |g' -e 's|^|#|g' \
    "$srcdir/glibc/localedata/SUPPORTED" >> "$pkgdir/data/data/com.termux/files/usr/glibc/etc/locale.gen"

  if check_option 'debug' n; then
    find "$pkgdir"/data/data/com.termux/files/usr/glibc/bin -type f -executable -exec strip $STRIP_BINARIES {} + 2> /dev/null || true
    find "$pkgdir"/data/data/com.termux/files/usr/glibc/lib -name '*.a' -type f -exec strip $STRIP_STATIC {} + 2> /dev/null || true

    # Do not strip these for gdb and valgrind functionality, but strip the rest
    find "$pkgdir"/data/data/com.termux/files/usr/glibc/lib \
      -not -name 'ld-*.so*' \
      -not -name 'libc.so*' \
      -not -name 'libpthread.so*' \
      -not -name 'libthread_db.so*' \
      -name '*.so*' -type f -exec strip $STRIP_SHARED {} + 2> /dev/null || true
  fi

  # Provide tracing probes to libstdc++ for exceptions, possibly for other
  # libraries too. Useful for gdb's catch command.
  install -Dm644 "$srcdir/sdt.h" "$pkgdir/data/data/com.termux/files/usr/glibc/include/sys/sdt.h"
  install -Dm644 "$srcdir/sdt-config.h" "$pkgdir/data/data/com.termux/files/usr/glibc/include/sys/sdt-config.h"

  # Provided by libxcrypt; keep the old shared library for backwards compatibility
  rm -f "$pkgdir"/data/data/com.termux/files/usr/glibc/include/crypt.h "$pkgdir"/data/data/com.termux/files/usr/glibc/lib/libcrypt.{a,so}

  sed -i 's|!/bin/bash|!/data/data/com.termux/files/usr/bin/bash|' "$pkgdir"/data/data/com.termux/files/usr/glibc/bin/*
  mkdir -p "$pkgdir"/data/data/com.termux/files/usr/bin
  install -m755 "$srcdir/grun" "$pkgdir/data/data/com.termux/files/usr/bin"
}
