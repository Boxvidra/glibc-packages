# Maintainer: St√©phane Gaudreault <stephane@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgname=keyutils-glibc
_tag='5678a1aae8834b5c16b5ed7dc72ef8836a29e122' # git rev-parse v${pkgver}
pkgver=1.6.3
pkgrel=0
pkgdesc='Linux Key Management Utilities'
arch=(any)
url='https://www.kernel.org/'
license=('GPL2' 'LGPL2.1')
makedepends=('git')
backup=('data/data/com.termux/files/usr/glibc/etc/request-key.conf')
validpgpkeys=('A86E54B0D5E1B4E0AB7C640FFBB7576BA7CB0B6B') # David Howells <dhowells@redhat.com>
source=("git+https://git.kernel.org/pub/scm/linux/kernel/git/dhowells/keyutils.git#tag=${_tag}?signed"
        'request-key.conf.patch' 'reproducible.patch')
sha256sums=('SKIP'
            '1ecf854011ce1a15e238653187fd6aed903b94c16d6a99bfca463bd8ca1c2f40'
            '7bb7400b2b8c8f0288c86ec9191f8964a1e682745a204013d5fc7c2e1a253d8e')

prepare() {
  cd keyutils
  # fix paths of binaries in /etc/request-key.conf
  /data/data/com.termux/files/usr/bin/patch -Np0 -i ../request-key.conf.patch

  # make keyutils reproducible
  /data/data/com.termux/files/usr/bin/patch -Np1 -i ../reproducible.patch
}

build() {
  cd keyutils

  case "$(uname -m)" in
    "aarch64")
      CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-aarch64.so.1"
      CXXFLAGS="${CFLAGS}"
      ;;
    "armv7l")
      CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-armhf.so.3"
      CXXFLAGS="${CFLAGS}"
      ;;
  esac

  make CFLAGS="${CFLAGS}" LDFLAGS="${LDFLAGS}" SBINDIR='/data/data/com.termux/files/usr/glibc/bin' BINDIR='/data/data/com.termux/files/usr/glibc/bin'
}

package() {
  cd keyutils
  mkdir -p "${pkgdir}/data/data/com.termux/files/usr/glibc"
  make DESTDIR="${pkgdir}/data/data/com.termux/files/usr/glibc" SBINDIR='/bin' BINDIR='/bin' LIBDIR='/lib' USRLIBDIR="/lib" INCLUDEDIR="/include" SHAREDIR="/share/keyutils" MANDIR="/share/man" install
}
