# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=krb5-glibc
_pkgvermajor=1.19
pkgver=1.19.3
pkgrel=0
pkgdesc='The Kerberos network authentication system'
url='https://web.mit.edu/kerberos/'
arch=(any)
license=('custom')
makedepends=('perl')
backup=(
  'data/data/com.termux/files/usr/glibc/etc/krb5.conf'
  'data/data/com.termux/files/usr/glibc/var/lib/krb5kdc/kdc.conf'
)
options=('!emptydirs')
source=(https://web.mit.edu/kerberos/dist/krb5/${_pkgvermajor}/krb5-${pkgver}.tar.gz{,.asc}
        krb5-config_LDFLAGS.patch
        krb5-kadmind.service
        krb5-kdc.service
        krb5-kpropd.service
        krb5-kpropd@.service
        krb5-kpropd.socket)
sha512sums=('18235440d6f7d8a72c5d7ca5cd8c6465e8adf091d85c483225c7b00d64b4688c1c7924cb800c2fc17e590b2709f1a9de48e6ec79f6debd11dcb7d6fa16c6f351'
            'SKIP'
            '5a3782ff17b383f8cd0415fd13538ab56afd788130d6ad640e9f2682b7deaae7f25713ce358058ed771091040dccf62a3bc87e6fd473d505ec189a95debcc801'
            'd6c74c1a65e24605ef96124bc97279de193656ae01a56744c24cb0045a9f51625c15f03a7380933c1c9b3b4befaf120f19b0c0ad0a78ee968c61a0bf4ee2ce65'
            'd0f1d6ec62bb38d6b97edd68b4bf00ec88c55f13a8a9bb1759a9aab5d8a531f830a88b703f4da5efe29f12328fb91f81af0f6694326dda40051d29da2ce08ae6'
            '6966e0d8895f211d25f2366fa34e75ada180c721cd6bb2bc3500b9f46aca40058c56822541d25da11ffbd43a3d612d2f4051d0f5ee9636c6ecc530fa3f9a0257'
            'a9e5cce4be7e10050b0a70a7f23e105567f766dc31c76560977f584d20478c0289f5650d39c46201438e01ef290b2af36187cb334684a6f4cd7dbc6ddf9ee2e6'
            '4e7ae175425e0787a1d5ff959471a88bf5af4cd6e213dc6d4048902fab7547c1186a082370b523f9549f5096acfab1fb03b4839e42bd80dc539130ae4bb3ea55')
validpgpkeys=('2C732B1C0DBEF678AB3AF606A32F17FD0055C305'  # Tom Yu <tlyu@mit.edu>
              'C4493CB739F4A89F9852CBC20CBA08575F8372DF') # Greg Hudson <ghudson@mit.edu>

prepare() {
  cd krb5-${pkgver}

  # cf https://bugs.gentoo.org/show_bug.cgi?id=448778
  /data/data/com.termux/files/usr/bin/patch -Np1 < "${srcdir}"/krb5-config_LDFLAGS.patch

  # FS#25384
  sed -i "/KRB5ROOT=/s/\/local//" src/util/ac_check_krb5.m4
}

build() {
   cd krb5-${pkgver}/src

   case "$CARCH" in
      "aarch64")
         CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-aarch64.so.1"
         CXXFLAGS="${CFLAGS}"
         ;;
      "armv7h")
         CFLAGS="-Wl,-rpath=/data/data/com.termux/files/usr/glibc/lib -Wl,--dynamic-linker=/data/data/com.termux/files/usr/glibc/lib/ld-linux-armhf.so.3"
         CXXFLAGS="${CFLAGS}"
         ;;
   esac

   export CFLAGS+=" -fPIC -fno-strict-aliasing -fstack-protector-all"
   export CPPFLAGS+=" -I/usr/include/et"
   ./configure --prefix=/data/data/com.termux/files/usr/glibc \
               --sbindir=/data/data/com.termux/files/usr/glibc/bin \
               --sysconfdir=/data/data/com.termux/files/usr/glibc/etc \
               --localstatedir=/data/data/com.termux/files/usr/glibc/var/lib \
               --enable-shared \
               --with-system-et \
               --disable-rpath \
               --without-tcl \
               --enable-dns-for-realm \
               --with-ldap \
               --without-system-verto
   make
}

package() {
   cd krb5-${pkgver}/src
   make DESTDIR="${pkgdir}" EXAMPLEDIR=/data/data/com.termux/files/usr/glibc/share/doc/${pkgname}/examples install

   # Fix FS#29889
   install -m 644 plugins/kdb/ldap/libkdb_ldap/kerberos.{ldif,schema} \
     "${pkgdir}/data/data/com.termux/files/usr/glibc/share/doc/${pkgname}/examples"

   install -Dpm 644 config-files/krb5.conf -t "${pkgdir}/data/data/com.termux/files/usr/glibc/etc"
   install -Dpm 644 config-files/kdc.conf -t "${pkgdir}/data/data/com.termux/files/usr/glibc/var/lib/krb5kdc"

   install -Dm 644 util/ac_check_krb5.m4 -t "${pkgdir}/data/data/com.termux/files/usr/glibc/share/aclocal"

   install -Dm 644 "${srcdir}"/krb5-${pkgver}/NOTICE \
     "${pkgdir}/data/data/com.termux/files/usr/glibc/share/licenses/${pkgname}/LICENSE"

   # systemd stuff
   install -Dm 644 "${srcdir}"/krb5-{kadmind.service,kdc.service,kpropd.service,kpropd@.service,kpropd.socket} \
      -t "${pkgdir}/data/data/com.termux/files/usr/glibc/lib/systemd/system"
}

# vim: ts=2 sw=2 et:
